plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "jacoco"
    id "eclipse"
}

group "be.konekto"
version "2.3.5.RELEASE"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
    classDirectories.from(fileTree(dir: "build/classes/main", include: "freetimelabs/io/**"))
    sourceDirectories.from(fileTree(dir: "scr/main/java", include: "freetimelabs/io/**"))
}

dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:${version}")

    api "io.projectreactor:reactor-core"

    testImplementation "junit:junit"
    testImplementation "org.assertj:assertj-core"
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
            pom {
                name = "ReactorFX"
                description = "Reactive JavaFX event handling"
                url = "https://github.com/konekto-be/ReactorFX"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "shadskii"
                        name = "Jake Hassel"
                        email = "jakehasssel@gmail.com"
                    }
                    developer {
                        id = "thekalinga"
                        name = "Ashok Koyi"
                        url = "https://thekalinga.in"
                        organization = "1Ton technolgies"
                        organizationUrl = "https://1ton.in"
                    }
                    developer {
                        id = "laurentkonekto"
                        name = "Laurent Lamouline"
                        email = "laurent@konekto.be"
                        organization = "Konekto"
                        organizationUrl = "https://konekto.be"
                    }
                    developer {
                        id = "vincentkonekto"
                        name = "Vincent Nuttin"
                        email = "vincent@konekto.be"
                        organization = "Konekto"
                        organizationUrl = "https://konekto.be"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/konekto-be/ReactorFX.git"
                    developerConnection = "scm:git:https://github.com/konekto-be/ReactorFX.git"
                    url = "https://github.com/konekto-be/ReactorFX"
                }
            }
        }
    }
    repositories {
        maven {
            credentials(PasswordCredentials) {
                username = "${mavenUser}"
                password = "${mavenPassword}"
            }
            url = "https://konekto-maven-repo.appspot.com"
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}